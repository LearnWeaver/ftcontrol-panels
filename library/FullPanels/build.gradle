plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply from: '../build.dependencies.gradle'

android {
    namespace "com.bylazar.fullpanels"

    compileSdkVersion 35

    defaultConfig {
        minSdk 24
    }


    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

tasks.register('installSvelteApp', Exec) {
    dependsOn(clearAssets)
    workingDir 'web'

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'bun', 'install'
    } else {
        commandLine 'sh', '-c', 'bun install'
    }
}

tasks.register('buildSvelteApp', Exec) {
    dependsOn installSvelteApp
    workingDir 'web'

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'bun', 'run', 'build'
    } else {
        commandLine 'sh', '-c', 'bun run build'
    }
}

tasks.register('clearAssets', Delete) {
    delete fileTree('src/main/assets/web/plugins/*')
}

tasks.register('copySvelteToAssets', Copy) {
    dependsOn buildSvelteApp
    from 'web/dist'
    into 'src/main/assets/web/plugins/com.bylazar.fullpanels'
}

preBuild.dependsOn(copySvelteToAssets)


dependencies {
    api project(':Panels')
    api project(':OpModeControl')
    api project(':ExamplePlugin')
    api project(':Telemetry')
    api project(':Configurables')
    api project(':Themes')
    api project(':Capture')
    api project(':LimelightProxy')
    api project(':Gamepad')
    api project(':Field')
    api project(':Docs')
    api project(':Battery')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.bylazar'
                artifactId = 'fullpanels'
                version = '0.0.3'

                pom {
                    description = "All in one toolbox dashboard for FTC with core plugins."
                    name = "Full Panels"
                    url = "https://panels.bylazar.com"
                    developers {
                        developer {
                            id = "lazar"
                            name = "Lazar Dragos George"
                            email = "hi@bylazar.com"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "localDevRepo"
                url = uri("file://C:/Users/lazar/Documents/GitHub/ftcontrol-maven/dev")
            }
        }
    }
}